apply from: 'cnri.gradle'
apply plugin: 'java-library'

group = 'net.handle'
archivesBaseName = 'handle'
version = '9.3.1'
project.ext.omitRevision = true

ext.pomUrl = 'https://www.handle.net'
ext.pomDeveloperEmail = 'hdladmin@cnri.reston.va.us'

defaultTasks 'clean', 'serverDistTar'

repositories {
    maven { url 'https://handle.net/maven' } // for handle-admin-webapp
    maven { url 'https://download.oracle.com/maven' }  // for JE
    flatDir {
        dirs("/opt/hsj/handle-9.3.1/lib")
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
    }
}

// Set resources directories to be main directory.
// Gradle excludes *.java files in resource directories by default
sourceSets.main.resources {
    srcDirs = ['src/main/java']
    srcDir 'libs/src'
}
sourceSets.test.resources {
    srcDirs = ['src/test/java']
}
sourceSets.integrationTest.resources {
    srcDirs = ['src/integrationTest/java']
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    servletContainer
    compileClasspath.extendsFrom servletContainer
    jettyProxyJars
    jeUpgradeJars
    adminWar
}

dependencies {
    compile name: 'pidmrhdlproxy-0.1'

    compile 'com.google.code.gson:gson:2.10'
    compile 'net.cnri:cnriutil:2.0'
    compile 'commons-codec:commons-codec:1.13'

    // optional
    compile 'org.bouncycastle:bcprov-jdk15on:1.70'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.70'

    // server-only
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'com.sleepycat:je:7.5.11'

    // server-only optional
    compile 'com.github.oshi:oshi-core:3.13.3'
    compile 'org.slf4j:slf4j-api:1.7.30'
    runtime 'org.slf4j:slf4j-simple:1.7.30'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'net.minidev:json-smart:2.3'
    testCompile 'com.googlecode.json-simple:json-simple:1.1.1'
    testCompile 'com.nimbusds:nimbus-jose-jwt:2.26.1'

    servletContainer 'net.cnri:cnri-servlet-container:3.0.0'

    jettyProxyJars 'net.cnri:cnri-cors-filter:9.4.15'

    jeUpgradeJars 'com.sleepycat:je:4.1.27'
    jeUpgradeJars 'javax.transaction:jta:1.1'

    adminWar 'net.handle:handle-admin-webapp:0.9.6-beta@war'
}

task buildVersionInfo {
    doLast {
        String fullVersion
        if (project.ext.properties.containsKey('omitRevision')) {
            fullVersion = version
        } else {
            ByteArrayOutputStream idOut = new ByteArrayOutputStream()
            exec {
                ignoreExitValue true
                commandLine 'bash', '-c', 'PATH=/usr/local/bin:$PATH git rev-parse --short=12 HEAD || hg id -i'
                standardOutput = idOut
            }
            ByteArrayOutputStream dateOut = new ByteArrayOutputStream()
            exec {
                ignoreExitValue true
                environment 'TZ', 'UTC'
                // oddly, building on Windows seemed to need avoiding the double-quotes required by this command; hence the echo commands in bash
                commandLine 'bash', '-c', "PATH=/usr/local/bin:\$PATH git show -s --format=%cd --date=format:%Y%m%d%H%M%S HEAD || hg parents --template '{date(date|localdate,'`echo -e '\\x22'`%Y%m%d%H%M%S`echo -e '\\x22'`')}'"
                standardOutput = dateOut
            }
            String date = dateOut.toString().trim()
            if (date.isEmpty()) date = '?'
            String id = idOut.toString().trim()
            if (id.isEmpty()) id = '?'
            fullVersion = [version, date, id].join('.')
        }
        println 'Version ' + fullVersion
        File versionFile = new File('src/main/java/net/handle/server/Version.java')
        try {
           versionFile.text = '' +
                'package net.handle.server;\n' +
                'public class Version {\n' +
                '    public static final String version="' + fullVersion + '";\n' +
                '}'
        } catch (FileNotFoundException e) { }
    }
}
compileJava.dependsOn buildVersionInfo

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test

javadoc {
    include 'net/handle/hdllib/**'
    include 'net/handle/api/**'
    include 'net/handle/security/**'
    include 'net/handle/util/**'
    include 'net/handle/apps/batch/**'
    include 'net/handle/apps/simple/*InfoConverter*'
}

//// BEGIN jar/war targets
String buildDir = compileJava.getDestinationDir()

task admintool(type: Jar) {
    baseName 'admintool'
    dependsOn compileJava
    from(buildDir) {
        include 'net/cnri/guiutil/**'
        include 'net/handle/apps/admintool/**'
    }
    from('src/main/java') {
        include 'net/handle/apps/admintool/view/resources/**'
    }
    artifacts {
        archives admintool
    }
}

task oldadmintool(type: Jar) {
    baseName 'oldadmintool'
    dependsOn compileJava
    from(buildDir) {
        include 'net/handle/apps/gui/**'
    }
    from('src/main/java') {
        include 'net/handle/apps/gui/hadmin/icons/**'
        include 'net/handle/apps/gui/help/**'
    }
    artifacts {
        archives oldadmintool
    }
}

task distJar(type: Jar) {
    baseName 'handle'
    dependsOn compileJava
    from(buildDir) {
        exclude 'net/cnri/guiutil/**'
        exclude 'net/handle/apps/admintool/**'
        exclude 'net/handle/apps/gui/**'
    }
    from('src/main/java') {
        include 'net/handle/apps/servlet_proxy/resources/**'
        include 'net/handle/etc/**'
    }
    artifacts {
        archives distJar
    }
}

task clientSourcesJar(type: Jar, dependsOn: classes) {
    baseName 'handle-client'
    classifier = 'sources'
    from sourceSets.main.allSource, {
        exclude 'net/cnri/guiutil/**'
        exclude 'net/handle/apps/admintool/**'
        exclude 'net/handle/apps/gui/**'
        exclude 'net/handle/jdb/**'
        exclude 'net/handle/apps/servlet_proxy/**'
        exclude 'net/handle/apps/db_tool/**'
        exclude 'net/handle/apps/test/**'
        exclude 'net/handle/server/**'
        exclude 'net/handle/dnslib/**'
        exclude 'net/handle/apps/tools/**'
    }
    from sourceSets.main.allSource, {
        include 'net/handle/dnslib/**'
        exclude 'net/handle/dnslib/Storage*'
        exclude 'net/handle/dnslib/NameServer*'
    }
    from sourceSets.main.allSource, {
        include 'net/handle/apps/tools/**'
        exclude 'net/handle/apps/tools/*Storage*'
        exclude 'net/handle/apps/tools/Recover*'
        exclude 'net/handle/apps/tools/Split*'
        exclude 'net/handle/apps/tools/*JDB*'
    }
    artifacts {
        archives clientSourcesJar
    }
}

task clientJavadoc(type: Javadoc) {
    destinationDir = file("${project.docsDir}/clientJavadoc")
    classpath = project.sourceSets.main.compileClasspath
    title = 'handle-client ' + version + ' API'
    source = sourceSets.main.allJava.matching {
            exclude 'net/cnri/guiutil/**'
            exclude 'net/handle/apps/admintool/**'
            exclude 'net/handle/apps/gui/**'
            exclude 'net/handle/jdb/**'
            exclude 'net/handle/apps/servlet_proxy/**'
            exclude 'net/handle/apps/db_tool/**'
            exclude 'net/handle/apps/test/**'
            exclude 'net/handle/server/**'
            exclude 'net/handle/dnslib/**'
            exclude 'net/handle/apps/tools/**'
        } +
        sourceSets.main.allJava.matching {
            include 'net/handle/dnslib/**'
            exclude 'net/handle/dnslib/Storage*'
            exclude 'net/handle/dnslib/NameServer*'
        } +
        sourceSets.main.allJava.matching {
            include 'net/handle/apps/tools/**'
            exclude 'net/handle/apps/tools/*Storage*'
            exclude 'net/handle/apps/tools/Recover*'
            exclude 'net/handle/apps/tools/Split*'
            exclude 'net/handle/apps/tools/*JDB*'
        }
}

task clientJavadocJar(type: Jar, dependsOn: clientJavadoc) {
    baseName 'handle-client'
    classifier = 'javadoc'
    from clientJavadoc.destinationDir
    artifacts {
        archives clientJavadocJar
    }
}

task clientDistJar(type: Jar) {
    baseName 'handle-client'
    dependsOn compileJava
    from(buildDir) {
        exclude 'net/cnri/guiutil/**'
        exclude 'net/handle/apps/admintool/**'
        exclude 'net/handle/apps/gui/**'
        exclude 'net/handle/jdb/**'
        exclude 'net/handle/apps/servlet_proxy/**'
        exclude 'net/handle/apps/db_tool/**'
        exclude 'net/handle/apps/test/**'
        exclude 'net/handle/server/**'
        exclude 'net/handle/dnslib/**'
        exclude 'net/handle/apps/tools/**'
    }
    from(buildDir) {
        include 'net/handle/dnslib/**'
        exclude 'net/handle/dnslib/Storage*'
        exclude 'net/handle/dnslib/NameServer*'
    }
    from(buildDir) {
        include 'net/handle/apps/tools/**'
        exclude 'net/handle/apps/tools/*Storage*'
        exclude 'net/handle/apps/tools/Recover*'
        exclude 'net/handle/apps/tools/Split*'
        exclude 'net/handle/apps/tools/*JDB*'
    }
    from('src/main/java') {
        include 'net/handle/etc/**'
    }
    artifacts {
        archives clientDistJar
    }
}

task proxyJar(type: Jar) {
    baseName 'hdlproxy'
    classifier 'classes'
    dependsOn compileJava
    from(buildDir) {
        include 'net/cnri/**'
        exclude 'net/cnri/guiutil/**'
        include 'net/handle/apps/servlet_proxy/**'
        include 'net/handle/apps/simple/*InfoConverter*'
        include 'net/handle/hdllib/**'
        include 'net/handle/etc/**'
        include 'net/handle/security/**'
        include 'net/handle/util/**'
        include 'net/handle/server/servletcontainer/servlets/**'
        exclude 'net/handle/server/servletcontainer/servlets/SessionsServlet.class'
        include 'net/handle/server/servletcontainer/auth/**'
        exclude 'net/handle/server/servletcontainer/auth/StandardHandleAuthenticationFilter.class'
        include 'net/handle/server/servletcontainer/support/**'
        include 'net/handle/server/servletcontainer/HandleServerInterface.class'
        include 'net/handle/server/servletcontainer/AutoSelfSignedKeyManager.class'
        include 'net/handle/server/MonitorDaemon*'
        include 'net/handle/server/SystemInfoWrapper*'
        include 'net/handle/server/replication/NotifierInterface.class'
    }
    from('src/main/java') {
        include 'net/handle/apps/servlet_proxy/resources/**'
        include 'net/handle/etc/**'
    }
    artifacts {
        archives proxyJar
    }
}

task proxyWar(type: Jar) {
    baseName 'hdlproxy'
    dependsOn proxyJar
    extension 'war'
    from('src/main/java/net/handle/apps/servlet_proxy/resources') {
    }
    from(configurations.runtime) {
        into 'WEB-INF/lib'
        include 'commons-codec*.jar'
        include 'gson*.jar'
        include 'cnriutil*.jar'
    }
    from(configurations.jettyProxyJars) {
        into 'WEB-INF/lib'
    }
    from('build/libs') {
        into 'WEB-INF/lib'
        include 'hdlproxy-' + version + '-classes.jar'
    }
    artifacts {
        archives proxyWar
    }
}
//// END jar/war targets

//// BEGIN src zips
task clientSrcDistExploded(type: Copy) {
    mustRunAfter clean, buildVersionInfo
    destinationDir project.distsDir
    String folderPath = 'handle-client-' + version + '-src'
    from('.') {
        into folderPath
        include 'build.gradle'
        include 'cnri.gradle'
        include 'settings.gradle'
        include 'gradlew*'
        include 'gradle/**'
        include 'scripts/unix/client/**'
        include 'scripts/windows/client/**'
        include 'scripts/launchers/*client*'
    }
    from('doc') {
        into folderPath + '/doc'
        include 'hcj-LICENSE.txt'
        include 'hcj-README.txt'
    }
    from('src/main/java') {
        into(folderPath + '/src/main/java')
        exclude 'net/handle/jdb/**'
        exclude 'net/handle/apps/servlet_proxy/**'
        exclude 'net/handle/apps/db_tool/**'
        exclude 'net/handle/apps/test/**'
        exclude 'net/handle/server/**'
        exclude 'net/handle/dnslib/**'
        exclude 'net/handle/apps/tools/**'
    }
    from('src/main/java') {
        into(folderPath + '/src/main/java')
        include 'net/handle/dnslib/**'
        exclude 'net/handle/dnslib/Storage*'
        exclude 'net/handle/dnslib/NameServer*'
    }
    from('src/main/java') {
        into(folderPath + '/src/main/java')
        include 'net/handle/apps/tools/**'
        exclude 'net/handle/apps/tools/*Storage*'
        exclude 'net/handle/apps/tools/Recover*'
        exclude 'net/handle/apps/tools/Split*'
        exclude 'net/handle/apps/tools/*JDB*'
    }
}

task clientSrcDistZip(type: Zip) {
    dependsOn clientSrcDistExploded
    baseName 'handle-client'
    classifier 'src'
    String folderPath = 'handle-client-' + version + '-src'
    from('build/distributions/' + folderPath) {
        into folderPath
    }
}

task proxySrcDistExploded(type: Copy) {
    mustRunAfter clean, buildVersionInfo
    destinationDir project.distsDir
    String folderPath = 'hdlproxy-' + version + '-src'
    from('.') {
        into folderPath
        include 'build.gradle'
        include 'cnri.gradle'
        include 'settings.gradle'
        include 'gradlew*'
        include 'gradle/**'
    }
    from('doc') {
        into folderPath + '/doc'
        include 'hps-LICENSE.txt'
        include 'hps-README.txt'
        include 'REST-API.txt'
    }
    from('src/main/java') {
        into(folderPath + '/src/main/java')
        include 'net/cnri/**'
        exclude 'net/cnri/guiutil/**'
        include 'net/handle/apps/servlet_proxy/**'
        include 'net/handle/apps/simple/*InfoConverter*'
        include 'net/handle/hdllib/**'
        include 'net/handle/etc/**'
        include 'net/handle/security/**'
        include 'net/handle/util/**'
        include 'net/handle/server/servletcontainer/servlets/**'
        exclude 'net/handle/server/servletcontainer/servlets/SessionsServlet.java'
        include 'net/handle/server/servletcontainer/auth/**'
        exclude 'net/handle/server/servletcontainer/auth/StandardHandleAuthenticationFilter.java'
        include 'net/handle/server/servletcontainer/support/**'
        include 'net/handle/server/servletcontainer/HandleServerInterface.java'
        include 'net/handle/server/servletcontainer/AutoSelfSignedKeyManager.java'
        include 'net/handle/server/MonitorDaemon*'
        include 'net/handle/server/SystemInfoWrapper*'
        include 'net/handle/server/replication/NotifierInterface.java'
    }
}

task proxySrcDistZip(type: Zip) {
    dependsOn proxySrcDistExploded
    baseName 'hdlproxy'
    classifier 'src'
    String folderPath = 'hdlproxy-' + version + '-src'
    from('build/distributions/' + folderPath) {
        into folderPath
    }
}

task serverSrcDistExploded(type: Copy) {
    mustRunAfter clean, buildVersionInfo
    destinationDir project.distsDir
    String folderPath = 'handle-' + version + '-src'
    from('.') {
        into folderPath
        include 'build.gradle'
        include 'cnri.gradle'
        include 'settings.gradle'
        include 'gradlew*'
        include 'gradle/**'
        include 'scripts/unix/**'
        include 'scripts/windows/**'
        include 'scripts/launchers/**'
        include 'src/main/java/**'
    }
    from('doc') {
        into folderPath + '/doc'
        include 'LICENSE.txt'
        include 'README.txt'
        include 'hcj-LICENSE.txt'
        include 'hcj-README.txt'
        include 'hps-LICENSE.txt'
        include 'hps-README.txt'
        include 'REST-API.txt'
        include 'Handle_Technical_Manual.pdf'
        include 'HandleTool_UserManual.pdf'
        include 'RELEASE_NOTES.pdf'
        include 'RELEASE_NOTES.txt'
        include 'jeUpgradeTool-README.txt'
    }
}

task serverSrcDistZip(type: Zip) {
    dependsOn serverSrcDistExploded
    baseName 'handle'
    classifier 'src'
    String folderPath = 'handle-' + version + '-src'
    from('build/distributions/' + folderPath) {
        into folderPath
    }
}
//// END src zips

//// BEGIN distribution targets
task clientDistExploded(type: Copy) {
    dependsOn clean, admintool, oldadmintool, clientDistJar, javadoc, clientSrcDistZip
    String baseName = 'handle-client'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    // license and readme
    from('doc') {
        into folderPath
        include 'hcj-LICENSE.txt'
        rename 'hcj-LICENSE.txt', 'LICENSE.txt'
        include 'hcj-README.txt'
        rename 'hcj-README.txt', 'README.txt'
    }
    // runtime dependency jars
    from(configurations.runtime) {
        into folderPath + '/lib'
        include 'commons-codec*.jar'
        include 'gson*.jar'
        include 'cnriutil*.jar'
    }
    // other tool jars
    from('build/libs') {
        into folderPath + '/lib'
        include 'admintool-' + version + '.jar'
        include 'handle-client-' + version + '.jar'
        include 'oldadmintool-' + version + '.jar'
    }
    // src zip
    from('build/distributions') {
        into folderPath
        include 'handle-client-' + version + '-src.zip'
    }
    // javadoc
    from('build/docs/javadoc') {
        into folderPath + '/doc/apidoc'
    }
    // *ix scripts
    from('scripts/launchers/unix-client') {
        into folderPath + '/bin'
        rename { String fileName -> return 'hdl' }
    }
    from('scripts/unix/client') {
        into folderPath + '/bin'
    }
    // windows scripts
    from('scripts/launchers/windows-client.bat') {
        into folderPath + '/bin'
        rename { String fileName -> return 'hdl.bat' }
    }
    from('scripts/windows/client') {
        into folderPath + '/bin'
    }
}

task clientDistTar(type: Tar) {
    dependsOn clientDistExploded
    compression Compression.GZIP
    extension 'tar.gz'
    baseName 'handle-client'
    classifier 'distribution'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    from('build/distributions/' + folderPath) {
        into folderPath
    }
    artifacts {
        archives clientDistTar
    }
}

task proxyDistExploded(type: Copy) {
    dependsOn clean, proxySrcDistZip, proxyWar
    String baseName = 'hdlproxy'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    // license and readme
    from('doc') {
        into folderPath
        include 'hps-LICENSE.txt'
        rename 'hps-LICENSE.txt', 'LICENSE.txt'
        include 'hps-README.txt'
        rename 'hps-README.txt', 'README.txt'
        include 'REST-API.txt'
    }
    // src zip
    from('build/distributions') {
        into folderPath
        include 'hdlproxy-' + version + '-src.zip'
    }
    // proxy war
    from('build/libs') {
        into folderPath
        include 'hdlproxy-' + version + '.war'
    }
}

task proxyDistTar(type: Tar) {
    dependsOn proxyDistExploded
    compression Compression.GZIP
    extension 'tar.gz'
    baseName 'hdlproxy'
    classifier 'distribution'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    from('build/distributions/' + folderPath) {
        into folderPath
    }
    artifacts {
        archives proxyDistTar
    }
}

task serverDistExploded(type: Copy) {
    description 'Main binary distribution'
    dependsOn clean, admintool, oldadmintool, distJar, javadoc, serverSrcDistZip
    String baseName = 'handle'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    // license and readme
    from('doc') {
        into folderPath
        include 'LICENSE.txt'
        include 'README.txt'
        include 'RELEASE_NOTES.txt'
    }
    // docs
    from('doc') {
        into folderPath + '/doc'
        include 'Handle_Technical_Manual.pdf'
        include 'HandleTool_UserManual.pdf'
        include 'RELEASE_NOTES.pdf'
        include 'RELEASE_NOTES.txt'
    }
    // runtime dependency jars
    from(configurations.runtime) {
        into folderPath + '/lib'
    }
    from(configurations.servletContainer - configurations.runtime) {
        into folderPath + '/lib/servletContainer'
    }
    from(configurations.jeUpgradeJars) {
        into folderPath + '/jeUpgradeTool'
    }
    from('doc') {
        into folderPath + '/jeUpgradeTool'
        include 'jeUpgradeTool-README.txt'
    }
    // other tool jars
    from('build/libs') {
        into folderPath + '/lib'
        include 'handle-' + version + '.jar'
        include 'admintool-' + version + '.jar'
        include 'oldadmintool-' + version + '.jar'
    }
    // src zip
    from('build/distributions') {
        into folderPath
        include 'handle-' + version + '-src.zip'
    }
    // javadoc
    from('build/docs/javadoc') {
        into folderPath + '/doc/apidoc'
    }
    // *ix scripts
    from('scripts/launchers/unix-server') {
        into folderPath + '/bin'
        rename { String fileName -> return 'hdl' }
    }
    from('scripts/unix/client') {
        into folderPath + '/bin'
    }
    from('scripts/unix/server') {
        into folderPath + '/bin'
    }
    // windows scripts
    from('scripts/launchers/windows-server.bat') {
        into folderPath + '/bin'
        rename { String fileName -> return 'hdl.bat' }
    }
    from('scripts/windows/client') {
        into folderPath + '/bin'
    }
    from('scripts/windows/server') {
        into folderPath + '/bin'
    }
    from(configurations.adminWar) {
        into folderPath
        rename 'handle-admin-webapp-.*.war', 'admin.war'
    }
}

task serverDistTar(type: Tar) {
    description 'Main binary distribution'
    dependsOn serverDistExploded
    compression Compression.GZIP
    extension 'tar.gz'
    baseName 'handle'
    classifier 'distribution'
    String folderPath = baseName + '-' + version
    destinationDir project.distsDir
    outputs.upToDateWhen { false }
    from('build/distributions/' + folderPath) {
        into folderPath
    }
    artifacts {
        archives serverDistTar
    }
}

//// END distribution targets

uploadArchives {
    repositories.mavenDeployer {
        if (!project.hasProperty('mavenCentral')) {
            addFilter('handle') {artifact, file ->
                artifact.name == 'handle'
            }
            addFilter('hdlproxy') {artifact, file ->
                artifact.name == 'hdlproxy'
            }
        }
        if (project.hasProperty('mavenCentral') || version.endsWith('-SNAPSHOT')) {
            addFilter('handle-client') {artifact, file ->
                artifact.name == 'handle-client'
            }
            pom('handle-client').whenConfigured { pom ->
                pom.dependencies.removeAll { dep -> dep.scope == 'test' }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.python' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.bouncycastle' }.each { dep -> dep.optional = true }
                pom.dependencies.removeAll { dep -> dep.groupId == 'javax.servlet' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'net.cnri' && dep.artifactId == 'cnri-servlet-container' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'com.sleepycat' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'com.github.oshi' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'org.slf4j' }
                project.ext.configurePom(pom)
                pom.project {
                    name 'Handle Client'
                    description 'HDL Client Library'
                    licenses {
                        license {
                            name 'CNRI License Agreement for HDL Client Library (ver. 9) â€“ Java Version'
                            url 'https://hdl.handle.net/20.1000/114'
                        }
                    }
                }
            }
        }
        if (!project.hasProperty('mavenCentral')) {
            pom('hdlproxy').whenConfigured { pom ->
                pom.dependencies.removeAll { dep -> dep.scope == 'test' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'org.python' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'org.bouncycastle' }
                pom.dependencies.findAll { dep -> dep.groupId == 'javax.servlet' }.each { dep -> dep.scope = 'provided' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'net.cnri' && dep.artifactId == 'cnri-servlet-container' }
                pom.dependencies.removeAll { dep -> dep.groupId == 'com.sleepycat' }
                pom.dependencies.findAll { dep -> dep.groupId == 'com.github.oshi' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.slf4j' }.each { dep -> dep.optional = true }
                pom.withXml {
                    def dependenciesNode = asNode().get('dependencies')[0]
                    rootProject.configurations.jettyProxyJars.allDependencies.each { dep ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dep.group)
                        dependencyNode.appendNode('artifactId', dep.name)
                        dependencyNode.appendNode('version', dep.version)
                        dependencyNode.appendNode('scope', 'runtime')
                    }
                }
                project.ext.configurePom(pom)
                pom.project {
                    name 'Handle Proxy'
                    description 'HDL.NET Proxy Servlet'
                    licenses {
                        license {
                            name 'CNRI License Agreement for HDL.Net Proxy Servlet (ver. 9)'
                            url 'https://hdl.handle.net/20.1000/115'
                        }
                    }
                }
            }
            pom('handle').whenConfigured { pom ->
                pom.dependencies.removeAll { dep -> dep.scope == 'test' }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.python' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.bouncycastle' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'javax.servlet' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'net.cnri' && dep.artifactId == 'cnri-servlet-container' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'com.sleepycat' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'com.github.oshi' }.each { dep -> dep.optional = true }
                pom.dependencies.findAll { dep -> dep.groupId == 'org.slf4j' }.each { dep -> dep.optional = true }
                pom.withXml {
                    def dependenciesNode = asNode().get('dependencies')[0]
                    rootProject.configurations.servletContainer.allDependencies.each { dep ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dep.group)
                        dependencyNode.appendNode('artifactId', dep.name)
                        dependencyNode.appendNode('version', dep.version)
                        dependencyNode.appendNode('scope', 'compile')
                        dependencyNode.appendNode('optional', 'true')
                    }
                }
                project.ext.configurePom(pom)
                pom.project {
                    name 'Handle Server'
                    description 'CNRI Handle.Net Server Software'
                    licenses {
                        license {
                            name 'Handle.Net Public License Agreement (Ver.2)'
                            url 'https://hdl.handle.net/20.1000/112'
                        }
                    }
                }
            }
        }
    }
}
